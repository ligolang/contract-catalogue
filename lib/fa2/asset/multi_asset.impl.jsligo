#import "../common/assertions.jsligo" "Assertions"
#import "../common/errors.mligo" "Errors"
#import "../common/tzip12.datatypes.jsligo" "TZIP12"
#import "../common/tzip16.datatypes.jsligo" "TZIP16"
#import "./extendable_multi_asset.impl.jsligo" "MultiAssetExtendable"

export type ledger = MultiAssetExtendable.ledger;
export type operator = MultiAssetExtendable.operator;
export type operators = MultiAssetExtendable.operators;
export type storage = {
   ledger : ledger,
   operators : operators,
   token_metadata : TZIP12.tokenMetadata,
   metadata : TZIP16.metadata,
}

type ret = [list<operation>, storage];

@inline
const lift = (s: storage): MultiAssetExtendable.storage<unit> => {
  return {
    extension: unit,
    ledger: s.ledger,
    operators: s.operators,
    token_metadata: s.token_metadata,
    metadata: s.metadata
  };
}

@inline
const unlift = ([ops, s]: [list<operation>, MultiAssetExtendable.storage<unit>]): ret => {
  let storage = {
    ledger: s.ledger,
    operators: s.operators,
    token_metadata: s.token_metadata,
    metadata: s.metadata
  };
  return [ops, storage];
}

@entry
const transfer = (t: TZIP12.transfer, s: storage): ret =>
  unlift(MultiAssetExtendable.transfer(t, lift(s)))

@entry
const balance_of = (b: TZIP12.balance_of, s: storage): ret =>
  unlift(MultiAssetExtendable.balance_of(b, lift(s)))

@entry
const update_operators = (updates: TZIP12.update_operators, s: storage): ret =>
  unlift(MultiAssetExtendable.update_operators(updates, lift(s)))

@view
const get_balance = (p: [address, nat], s: storage): nat =>
  MultiAssetExtendable.get_balance(p, lift(s))

@view
const total_supply = (token_id: nat, s: storage): nat =>
  MultiAssetExtendable.total_supply(token_id, lift(s))

@view
const all_tokens = (_: unit, s: storage): set<nat> =>
  MultiAssetExtendable.all_tokens(unit, lift(s))

@view
const is_operator = (op: TZIP12.operator, s: storage): bool =>
  MultiAssetExtendable.is_operator(op, lift(s))

@view
const token_metadata = (p: nat, s: storage): TZIP12.tokenMetadataData =>
  MultiAssetExtendable.token_metadata(p, lift(s))
